---
id: 20221102172812_b041caf446d64b28
date: "2022-11-02"
aliases:
- real world haskell ch. 10 示例代码
- lambda 的范围
category:
- gist
gist:
  language:
  - haskell
  tags:
  - haskell/运算符
  - 函数推导
  - haskell
  - lambda
tc:
- gist
---

这个是 real world haskell 第十章的内容,
    看的时候有一段没有特别理解, 记录一下.

```haskell
import qualified Data.ByteString.Lazy.Char8 as L8
import qualified Data.ByteString.Lazy as L
import Data.Char (isSpace)

data Greymap = Greymap {
      greyWidth :: Int
    , greyHeight :: Int
    , greyMax :: Int
    , greyData :: L.ByteString
    } deriving (Eq)

instance Show Greymap where
    show (Greymap w h m _) = "Greymap " ++ show w ++ "x" ++ show h ++ " " ++ show m

matchHeader prefix str
    | prefix `L8.isPrefixOf` str
        = Just (L8.dropWhile isSpace (L.drop (L.length prefix) str))
    | otherwise
        = Nothing

-- 结合和优先级 infixl 9
(>>?) :: Maybe a -> (a -> Maybe b) -> Maybe b
Nothing >>? _ = Nothing
Just v  >>? f = f v

-- L.ByteString -> Maybe (Int, L.ByteString)
getNat s = case L8.readInt s of
             Nothing -> Nothing
             Just (num,rest)
                 | num <= 0    -> Nothing
                 | otherwise   -> Just (num, L8.dropWhile isSpace rest)

-- Int -> L.ByteString -> Maybe (L.ByteString, L.ByteString)
getBytes n str = let count           = fromIntegral n
                     both@(prefix,_) = L.splitAt count str
                 in if L.length prefix < count
                    then Nothing
                    else Just both


skipSpace :: (a, L.ByteString) -> Maybe (a, L.ByteString)
skipSpace (a, s) = Just (a, L8.dropWhile isSpace s)
```

最开始我理解的 `\(width, s0) -> ...` 这个 lambda 表达式中 width 作用域就是在 `->` 后的一句,
    但这里是可以直接在最后一句中使用, 让我感觉不和谐.

只能选择测试看看,
    自己写了一个简单的 demo 作用域是 `->` 之后的句子,
    但这个例子中实际的 lambda 比较大,
    不是只有 `getNat s0` 而是从 `getNat s0 >>? 到结尾` 都属于 lambda 的一部分.

lambda 并不需要用括号括起来, 是尽可能的向后匹配到结束.

```haskell
parseP5_take2 :: L.ByteString -> Maybe (Greymap, L.ByteString)
parseP5_take2 s =
    matchHeader (L8.pack "P5") s      >>?
    \s -> skipSpace ((), s)           >>?
    (getNat . snd)                    >>?
    skipSpace                         >>?
    \(width, s0) ->   getNat s0         >>?
    skipSpace                         >>?
    \(height, s1) ->  getNat s1         >>?
    \(maxGrey, s2) -> getBytes 1 s2     >>?
    (getBytes (width * height) . snd) >>?
    \(bitmap, s3) -> Just (Greymap width height maxGrey bitmap, s3)

-- 测试输出
-- parseP5_take2 (L8.pack "P5 2 2 2 12345")
-- Just (Greymap 2x2 2,"")
```

可以补充括号来重新展示看看
```haskell
parseP5_take2 :: L.ByteString -> Maybe (Greymap, L.ByteString)
parseP5_take2 s =
    matchHeader (L8.pack "P5") s  >>?
    (\s -> skipSpace ((), s)      >>?
        (getNat . snd)            >>?
        skipSpace                 >>?
        (\(width, s0) -> getNat s0   >>?
            skipSpace                  >>?
            (\(height, s1) -> getNat s1               >>?
                (\(maxGrey, s2) -> getBytes 1 s2      >>?
                    (getBytes (width * height) . snd) >>?
                    (\(bitmap, s3) -> Just (Greymap width height maxGrey bitmap, s3)
                    )
                )
            )
        )
    )
```

原始代码给我一种面条代码的感觉,
    要准确判断结合和优先级.